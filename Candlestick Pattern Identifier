//@version=4
//Created by RSirigiri 

//Description:  
//  The Candlestick Pattern Identifier automatically displays candlestick formations with a high probability of success 
//  based on findings by Thomas N. Bulkowski

//Methodology: 
//  Success rate: I searched for patterns with a success rate of 66% in at least one market (Bull or Bear)
//  Frequency: I looked for patterns with a sample size of 10 or higher to avoid rare patterns  
//  Formulas: I created formulas with both strict and lax guidelines by using strict guidelines to objectively 
//            identify patterns and lax guidelines to increase the frequency of these patterns being detected

study("Candlestick Pattern Identifier", overlay = true)

//"open" is the share price at the start of market hours 
open_today = open 
open_1_day_ago = open[1]
open_2_days_ago = open[2]
open_3_days_ago = open[3]

//"close" is the share price at the end of market hours 
close_today = close
close_1_day_ago = close[1]
close_2_days_ago = close[2]
close_3_days_ago = close[3]

//"high" is the highest a share price has gone during market hours 
high_today = high
high_1_day_ago = high[1]
high_2_days_ago = high[2]
high_3_days_ago = high[3]

//"low" is the lowest a share price has gone during market hours 
low_today = low 
low_1_day_ago = low[1] 
low_2_days_ago = low[2]
low_3_days_ago = low[3]

//Green candles signify a time period where the share price has gone up 
green_candle_today = open < close
green_candle_1_day_ago = open[1] < close[1]
green_candle_2_days_ago = open[2] < close[2]
green_candle_3_days_ago = open[3] < close[3]

//Red candles signify a time period where the share price has gone down 
red_candle_today = open > close
red_candle_1_day_ago = open[1] > close[1]
red_candle_2_days_ago = open[2] > close[2]
red_candle_3_days_ago = open[3] > close[3]

//Doji's are candles representing indecision and have thin bodies (represented by the small gap between the open and close of a candle) 
doji_1_day_ago_green = (green_candle_1_day_ago) and 
 close_1_day_ago - open_1_day_ago <= (close_1_day_ago + open_1_day_ago) * 0.001 
doji_1_day_ago_red = (red_candle_1_day_ago) and 
 open_1_day_ago - close_1_day_ago <= (open_1_day_ago + close_1_day_ago) * 0.001 
 
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//                                             BULLISH CONTINUATION PATTERNS                                      //    
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//Bullish Continuation pattern with 77% success in Bull markets and 75% in Bear markets
Deliberation = (green_candle_2_days_ago and green_candle_1_day_ago and green_candle_today) 
 and (close_2_days_ago < open_1_day_ago) and (close_1_day_ago < open_today)  
 and (high_2_days_ago - low_2_days_ago) > (high_1_day_ago - low_1_day_ago)  
 and (high_1_day_ago - low_1_day_ago) > (high_today - low_today)  
plotshape(Deliberation, title = "Deliberation", text = "·\n·\n·\nDeliberation", textcolor = color.green, 
 style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Continuation pattern with 75% success in Bull markets and 72% in Bear markets 
Rising_Window = (green_candle_today)  
 and (low_today - high_1_day_ago > close_1_day_ago - open_1_day_ago)  
 and (low_today - high_1_day_ago > open_1_day_ago - close_1_day_ago)
 and (close_today - open_today > high_1_day_ago - low_1_day_ago) 
 and (low_today > high_1_day_ago)  
plotshape(Rising_Window, title = "Rising Window", text = "·\nRising\nWindow", textcolor = color.green, 
 style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)
 
//Bullish Continuation with 72% success in Bull markets and 69% in Bear markets 
Separating_Lines_Bullish = (red_candle_1_day_ago and green_candle_today)  
 and (open_1_day_ago == open_today) 
 and (close_1_day_ago < low_today)  
 and (high_1_day_ago < close_today)
plotshape(Separating_Lines_Bullish, title = "Separating Lines Bullish", text = "·\n·\nSeparating\nLines\nBullish", textcolor = color.green, 
 style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//                                             BULLISH REVERSAL PATTERNS                                          //
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//Bullish Reversal with 84% success in Bull markets 77% in Bear markets 
Three_Line_Strike_Bullish = (red_candle_3_days_ago and red_candle_2_days_ago and red_candle_1_day_ago and green_candle_today)  
 and (close_3_days_ago >= open_2_days_ago) and (close_2_days_ago >= open_1_day_ago)  
 and (open_today <= close_1_day_ago) and (close_today >= open_3_days_ago)
plotshape(Three_Line_Strike_Bullish, title = "Three Line Strike Bullish", text = "·\n·\n·\n·\nThree\nLine\nStrike\nBullish", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Reversal pattern with 82% success in Bull markets and 84% in Bear markets
Three_White_Soldiers = (green_candle_2_days_ago and green_candle_1_day_ago and green_candle_today)  
 and (high_2_days_ago <= close_1_day_ago and high_1_day_ago <= close_today)  
 and (close_2_days_ago >= open_1_day_ago and close_1_day_ago >= open_today)  
 and (open_2_days_ago <= low_1_day_ago and open_1_day_ago <= low_today)  
plotshape(Three_White_Soldiers, title = "Three White Soldiers", text = "·\n·\n·\nThree\nWhite\nSoldiers", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green, offset = 0)

//Bullish Reversal pattern with 78% success in Bull markets and 65% in Bear markets. 
Morning_Star = (red_candle_2_days_ago and green_candle_1_day_ago and green_candle_today)  
 and (close_2_days_ago >= close_1_day_ago and close_1_day_ago <= open_today)  
 and (high_1_day_ago - low_1_day_ago < high_2_days_ago - low_2_days_ago) 
 and (high_1_day_ago - low_1_day_ago < high_today - low_today)  
plotshape(Morning_Star, title = "Morning Star", text = "·\n·\n·\nMorning\nStar", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Reversal with 76% success in Bull markets and 71% in Bear markets 
Morning_Doji_Star = (red_candle_2_days_ago and doji_1_day_ago_green or doji_1_day_ago_red and green_candle_today)  
 and (close_2_days_ago >= high_1_day_ago) 
 and (open_today >= high_1_day_ago)
plotshape(Morning_Doji_Star, title = "Morning Doji Star", text = "·\n·\n·\nMorning\nDoji\nStar", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Reversal with 75% success in Bull markets and 74% in Bear markets 
Three_Outside_Up = (red_candle_3_days_ago and red_candle_2_days_ago and green_candle_1_day_ago and green_candle_today) 
 and (open_1_day_ago <= low_2_days_ago 
 and close_1_day_ago >= high_2_days_ago) 
 and (close_today >= close_1_day_ago) 
plotshape(Three_Outside_Up, title = "Three Outside Up", text = "·\n·\n·\n·\nThree\nOutside\nUp", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Reversal pattern with 71% success in Bull and Bear markets 
Bullish_Belt_Hold = (red_candle_1_day_ago and green_candle_today)  
 and (open_today == low_today and open_today <= low_1_day_ago)  
 and (close_today - open_today >= high_today - close_today)  
 and (close_today <= open_1_day_ago)
plotshape(Bullish_Belt_Hold, title = "Bullish Belt Hold", text = "·\n·\nBullish\nBelt\nHold", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Reversal with 70% success in Bull markets and 55% in Bear markets 
Abandoned_Baby_Bullish = (red_candle_2_days_ago and doji_1_day_ago_green and green_candle_today)  
 and (high_1_day_ago < low_2_days_ago)  
 and (high_1_day_ago < low_today)
plotshape(Abandoned_Baby_Bullish, title = "Abandoned Baby Bullish", text = "·\n·\n·\nAbandoned\nBaby\nBullish", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Reversal pattern with 66% success in Bull markets and 67% in Bear markets 
Above_The_Stomach = (red_candle_3_days_ago and red_candle_2_days_ago and red_candle_1_day_ago and green_candle_today) 
 and (high_1_day_ago <= close_today)  
 and (close_1_day_ago <= open_today) 
 and (open_1_day_ago >= open_today)
plotshape(Above_The_Stomach, title = "Above The Stomach", text = "·\n·\n·\n·\nAbove\nThe\nStomach", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

//Bullish Reversal with 66% success in Bull markets and 63% in Bear markets 
Takuri_Line = (red_candle_3_days_ago and red_candle_2_days_ago and red_candle_1_day_ago and red_candle_today) 
 and (high_today - open_today) <= (open_today - close_today) 
 and (open_today - close_today) * 2.5 <= (close_today - low_today)
plotshape(Takuri_Line, title = "Takuri Line", text = "·\n·\n·\n·\nTakuri\nLine", 
 textcolor = color.green, style = shape.triangleup, size = size.auto, location = location.belowbar, color = color.green)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//                                             BEARISH CONTINUATION PATTERNS                                      //
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//Bearish Coninuation with 76% success in Bear markets and 63% in Bull markets 
Separating_Lines_Bearish = (green_candle_1_day_ago and red_candle_today)  
 and (open_today == open_1_day_ago) 
 and (close_1_day_ago > high_today)  
 and (low_1_day_ago > close_today)
plotshape(Separating_Lines_Bearish, title = "Separating Lines Bearish", text = "Separating\nLines\nBearish\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Continuation pattern with 73% success in Bear markets and 67% in Bull markets 
Falling_Window = (red_candle_today)  
 and (low_1_day_ago - high_today > high_1_day_ago - low_1_day_ago) 
 and (open_today - close_today > high_1_day_ago - low_1_day_ago)  
 and (high_today < low_1_day_ago)  
plotshape(Falling_Window, title = "Falling Window", text = "Falling\nWindow\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Continuation pattern with 69% success in Bear markets and 68% in Bull markets 
Two_Black_Gapping = (green_candle_3_days_ago and green_candle_2_days_ago and red_candle_1_day_ago and red_candle_today)  
 and (high_1_day_ago < low_2_days_ago) 
 and (open_today <= close_1_day_ago)
plotshape(Two_Black_Gapping, title = "Two Black Gapping", text = "Two\nBlack\nGapping\n·\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Continuation pattern with 67% success in Bear markets and 65% in Bull markets
Inverted_Hammer = (red_candle_1_day_ago and green_candle_today)  
 and (close_today - open_today >= open_today - low_today)  
 and (close_today <= (high_today + low_today) * 0.5)  
 and (close_today <= low_1_day_ago)
plotshape(Inverted_Hammer, title = "Inverted Hammer", text = "Inverted Hammer\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.belowbar, color = color.red)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//                                             BEARISH REVERSAL PATTERNS                                          //
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//Bearish Reversal with 83% success in Bear markets and 65% with Bull markets 
Three_Line_Strike_Bearish = (green_candle_3_days_ago and green_candle_2_days_ago and green_candle_1_day_ago and red_candle_today)  
 and (close_3_days_ago <= open_2_days_ago) and (close_2_days_ago <= open_1_day_ago)  
 and (open_today > close_1_day_ago) and (close_today <= open_3_days_ago)
plotshape(Three_Line_Strike_Bearish, title = "Three Line Strike Bearish", text = "Three\nLine\nStrike\nBearish\n·\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal 82% success in Bear markets and 79% in Bull markets
Bearish_Engulfing = (green_candle_2_days_ago and green_candle_1_day_ago and red_candle_today) 
 and (open_today >= high_1_day_ago and close_today <= low_1_day_ago)
plotshape(Bearish_Engulfing, title = "Bearish Engulfing", text = "Bearish\nEngulfing\n·\n·\n·️",
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal pattern with 79% success in Bear markets and 78% in Bull markets
Three_Black_Crows = (red_candle_2_days_ago and red_candle_1_day_ago and red_candle_today)  
 and (open_1_day_ago >= close_2_days_ago and open_today >= close_1_day_ago)  
 and (open_2_days_ago >= high_1_day_ago and open_1_day_ago >= high_today)  
 and (low_2_days_ago >= close_1_day_ago and low_1_day_ago >= close_today) 
plotshape(Three_Black_Crows, title = "Three Black Crows", text = "Three\nBlack\nCrows\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal with 72% success in Bear markets and 79% in Bull markets 
Identical_Three_Crows = (red_candle_2_days_ago and red_candle_1_day_ago and red_candle_today)  
 and (close_2_days_ago >= open_1_day_ago) and (close_1_day_ago >= open_today) 
 and (low_2_days_ago <= open_1_day_ago) and (low_1_day_ago <= open_today) 
 and (open_2_days_ago >= high_1_day_ago) and (open_1_day_ago >= high_today) 
 and (low_2_days_ago >= close_1_day_ago) and (low_1_day_ago >= open_today)
plotshape(Identical_Three_Crows, title = "Identical Three Crows", text = "Identical\nThree\nCrows\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal pattern with 72% of success in both Bull and Bear markets 
Evening_Star = (green_candle_2_days_ago and red_candle_1_day_ago and red_candle_today)  
 and (close_2_days_ago <= close_1_day_ago and close_1_day_ago >= open_today)  
 and (high_1_day_ago - low_1_day_ago < high_2_days_ago - low_2_days_ago) 
 and (high_1_day_ago - low_1_day_ago < high_today - low_today)  
plotshape(Evening_Star, title = "Evening Star", text = "Evening\nStar\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal with 71% success in both Bull and Bear markets 
Evening_Doji_Star = (green_candle_2_days_ago and doji_1_day_ago_red and red_candle_today)  
 and (close_2_days_ago <= low_1_day_ago) 
 and (open_today <= low_1_day_ago)
plotshape(Evening_Doji_Star, title = "Evening Doji Star", text = "Evening\nDoji\nStar\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal pattern with 70% success in Bear markets and 69% in Bull markets 
Three_Outside_Down = (green_candle_3_days_ago and green_candle_2_days_ago and red_candle_1_day_ago and red_candle_today) 
 and (open_1_day_ago >= high_2_days_ago 
 and close_1_day_ago <= low_2_days_ago)  
 and (close_today <= close_1_day_ago) 
plotshape(Three_Outside_Down, title = "Three Outside Down", text = "Three\nOutside\nDown\n·\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal pattern with 69% success in Bear markets and 68% in Bull markets 
Bearish_Belt_Hold = (green_candle_1_day_ago and red_candle_today)  
 and (high_today == open_today and open_today >= high_1_day_ago)  
 and (open_today - close_today >= close_today - low_today) 
 and (close_today <= close_1_day_ago)  
plotshape(Bearish_Belt_Hold, title = "Bearish Belt Hold", text = "Bearish\nBelt\nHold\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)

//Bearish Reversal with 52% success in Bear markets and 69% in Bull markets 
Abandoned_Baby_Bearish = (green_candle_2_days_ago and doji_1_day_ago_red and red_candle_today)  
 and (low_1_day_ago > high_2_days_ago)  
 and (low_1_day_ago > high_today)
plotshape(Abandoned_Baby_Bearish, title = "Abandoned Baby Bearish", text = "Abandoned\nBaby\nBearish\n·\n·\n·", 
 textcolor = color.red, style = shape.triangledown, size = size.auto, location = location.abovebar, color = color.red)
